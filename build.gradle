
group = 'com.amocrm'
version = '0.3.0'


buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}


ext {
    bintrayRepo = 'amocrm-client'
    bintrayName = 'amocrm-client'

    publishedGroupId = group
    libraryName = 'amocrm-client'
    artifact = 'amocrm-client'

    libraryDescription = 'AmoCRM API client based on Retrofit2 http library'

    siteUrl = 'https://github.com/syncro/amocrm-client'
    gitUrl = 'https://github.com/syncro/amocrm-client.git'

    libraryVersion = version

    developerId = 'syncro'
    developerName = 'Aleksey Oblozhko'
    developerEmail = 'oblozhko@gmail.com'

    licenseName = 'GNU Lesser General Public License, version 3'
    licenseUrl = 'https://opensource.org/licenses/LGPL-3.0'
    allLicenses = ["LGPL-3.0"]
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

jar {
    baseName = 'amocrm-client'
    version = version
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile("com.squareup.retrofit2:retrofit:2.8.1")
    compile("com.squareup.retrofit2:adapter-rxjava2:2.8.1")
    compile("com.squareup.retrofit2:converter-gson:2.8.1")
    compile("com.google.code.gson:gson:2.6.2")
    compile("com.squareup.okhttp3:logging-interceptor:3.6.0")
    compile("com.squareup.okhttp3:okhttp-urlconnection:3.6.0")
    compile("org.projectlombok:lombok:1.16.16")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

apply plugin: 'maven'


uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = group
        pom.artifactId = 'amocrm-client'
        pom.version = version
    }
}


apply plugin: 'com.jfrog.bintray'

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = 'syncro'
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = 'https://github.com/syncro/amocrm-client'
        licenses = ["LGPL-3.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                // optional GPG encryption. Default is false.
                sign = false
                //passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
